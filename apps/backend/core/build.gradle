plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.github.spotbugs' version '6.0.26'
	id 'org.ec4j.editorconfig' version '0.1.0'
	id 'com.google.protobuf' version '0.9.4'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.schemafy'
version = '0.0.1-SNAPSHOT'
description = 'Core module of the backend application'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	withJavadocJar()
	withSourcesJar()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
	set('grpcVersion', '1.68.2')
	set('protobufVersion', '4.29.2')
}

dependencies {
	// webflux
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

	// db
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	runtimeOnly "io.r2dbc:r2dbc-h2"
	runtimeOnly 'org.mariadb:r2dbc-mariadb:1.1.3'

	// security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.github.ben-manes.caffeine:caffeine'

	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'

	testImplementation 'org.mockito:mockito-core:5.2.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'

	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// grpc
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
	compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'
	compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	// doc
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-api:2.8.13'

	// grpc testing
	testImplementation "io.grpc:grpc-testing:${grpcVersion}"

	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

	// spotbugs
	spotbugs 'com.github.spotbugs:spotbugs:4.8.6'
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ["-parameters"]
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test

	attributes 'snippets': snippetsDir
}

// Production 빌드: DDL 포함 X
tasks.named('bootJar') {
	exclude('ddl/**')
}

spotbugs {
	toolVersion = '4.8.6'
	effort = 'max'
	reportLevel = 'medium'  // high, medium, low
	ignoreFailures = false  // CI에서 버그 발견 시 빌드 실패
	excludeFilter = file("$projectDir/src/main/resources/spotbugs/spotbugs-exclude.xml")
	omitVisitors = ['FindReturnRef']
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
	enabled = true
	classes = classes.filter {
		!it.path.contains('/lombok/') && !it.path.contains('/grpc/')
	}

	// Missing classes 경고 무시
	jvmArgs = [
		'-Dspotbugs.sf.comment=true',
		'-Dfindbugs.verbose=true'
	]
	maxHeapSize = '1024m'

	reports {
		html {
			required = true
			outputLocation = layout.buildDirectory.file('reports/spotbugs/main.html')
			stylesheet = 'fancy-hist.xsl'
		}
		xml {
			required = true
			outputLocation = layout.buildDirectory.file('reports/spotbugs/main.xml')
		}
		sarif {
			required = true
			outputLocation = layout.buildDirectory.file('reports/spotbugs/main.sarif')
		}
	}
}

tasks.named('spotbugsTest') {
	enabled = false
}

spotless {
	java {
		target 'src/**/*.java'
		eclipse().configFile('java-formatter.xml')
		removeUnusedImports()
		importOrder(
				'java',
				'jakarta',
				'org.springframework',
				'org',
				'com',
				'',
				'\\#'
		)
		trimTrailingWhitespace()
		endWithNewline()
	}
}

tasks.named('check') {
	dependsOn 'spotlessCheck'
}

// Create symlink to shared proto files
tasks.register('linkProtos') {
	doLast {
		def sharedProtoDir = file('../../../packages/proto-validation/protos')
		def targetProtoDir = file('src/main/proto')

		targetProtoDir.mkdirs()

		def sourceFile = new File(sharedProtoDir, 'validation.proto')
		def targetFile = new File(targetProtoDir, 'validation.proto')

		if (!targetFile.exists()) {
			def relativeSourcePath = targetProtoDir.toPath().relativize(sourceFile.toPath()).toString()
			if (System.properties['os.name'].toLowerCase().contains('windows')) {
				exec {
					workingDir targetProtoDir
					commandLine 'cmd', '/c', 'mklink',
						targetFile.name, relativeSourcePath
				}
			} else {
				exec {
					workingDir targetProtoDir
					commandLine 'ln', '-sf',
						relativeSourcePath, targetFile.name
				}
			}
		}
	}
}

tasks.named('generateProto') {
	dependsOn 'linkProtos'
}

// Protobuf configuration
protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDir 'build/generated/source/proto/main/grpc'
			srcDir 'build/generated/source/proto/main/java'
		}
		resources {
			srcDir 'src/main/resources'
			srcDir 'sql'
		}
	}
}

tasks.named('processResources') {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('compileJava') {
	dependsOn 'generateProto'
}

tasks.withType(Jar).configureEach {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
